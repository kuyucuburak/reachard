buildscript {
    def buildNumber = System.getenv("BUILD_NUMBER") as Integer ?: 1

    ext {
        versionMajor = 1
        versionMinor = 0
        versionPatch = 0

        versionBuild = buildNumber
        versionCode = versionMajor * 10000000 + versionMinor * 100000 + versionPatch * 1000 + versionBuild
        versionName = "$versionMajor.$versionMinor.$versionPatch"

        minSdkVersion = 21
        targetSdkVersion = 34
        compileSdkVersion = 34

        versions = [
                composeKotlinCompilerExtension: "1.5.5",
        ]
    }
}

plugins {
    alias libs.plugins.android.application apply false
    alias libs.plugins.android.library apply false
    alias libs.plugins.kotlin.android apply false
}

subprojects { project ->
    afterEvaluate {
        if (project.plugins.hasPlugin("com.android.library") || project.plugins.hasPlugin("com.android.application")) {
            project.android {
                compileSdk project.rootProject.compileSdkVersion

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }

                defaultConfig {
                    minSdk rootProject.minSdkVersion
                    targetSdk rootProject.targetSdkVersion
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_17
                }
            }
        }

        if (project.plugins.hasPlugin("com.android.library")) {
            project.android {
                buildTypes.configureEach {
                    consumerProguardFiles "consumer-rules.pro"
                }

                defaultConfig {
                    consumerProguardFiles("consumer-proguard-rules.pro")
                }

                publishing {
                    singleVariant("release") {
                        withSourcesJar()
                        withJavadocJar()
                    }
                }
            }

            project.publishing {
                publications {
                    maven(MavenPublication) {
                        artifactId project.name

                        afterEvaluate {
                            from components.release
                        }
                    }
                }
            }
        }
    }
}
