plugins {
    id "com.android.library"
    id "maven-publish"
    id "org.jetbrains.kotlin.android"
}

tasks.register("androidJavadocs", Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.configureEach { variant ->
        if (variant.name == "release") {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
    exclude "**/R.html", "**/R.*.html", "**/index.html"
}

tasks.register("androidJavadocsJar", Jar) {
    dependsOn androidJavadocs
    archiveClassifier.set("javadoc")
    from androidJavadocs.destinationDir
}

tasks.register("androidSourcesJar", Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

android {
    namespace "com.burakkuyucu.reachard"
    compileSdk 33

    defaultConfig {
        minSdkVersion 1
        targetSdkVersion 33

        consumerProguardFiles("consumer-proguard-rules.pro")
    }

    buildTypes.configureEach {
        consumerProguardFiles "consumer-rules.pro"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    resourcePrefix "reachard_di_"
}

dependencies {
    // Unit Test
    testImplementation "junit:junit:${versions.junit}"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = "com.kuyucuburak"
                artifactId = "reachard-di"

                afterEvaluate {
                    from components.release
                }
            }
        }
    }
}
